#
#   Configure zsh's completion control
#

# Rmdir only real directories
compctl -g '*(/)' rd rmdir dircmp

# Strip only executables
compctl -g '*(*)' strip

# kill takes signal names as the first argument after -, but job names after %
compctl -j -P % -x 's[-] p[1]' -k signals -- kill

# gzip files, but gzip -d only gzipped or compressed files
compctl -f -x 'R[-*d*,^*]' -g '*.gz *.z *.Z' + -g '*(-/)' -- gzip
compctl -g '*.gz *.z *.Z' + -g '*(-/)' gunzip   # zcat if you use GNU
compctl -g '*.Z' + -g '*(-/)' uncompress zmore  # zcat if you don't use GNU

# find is very system dependend, this one is for GNU find.
compctl -x 's[-]' -k "(daystart depth follow maxdepth mindepth mount noleaf \
version xdev amin anewer cmin cnewer ctime empty false fstype gid group ilname \
iname inum ipath iregex links lname mmin mtime name newer nouser nogroup path \
perm regex size true type uid used user xtype exec fls fprint fprint0 fprintf \
ok print print0 printf prune ls)" - \
'p[1]' -g '. .. *(-/)' - \
'c[-1,-anewer][-1,-cnewer][-1,-newer][-1,-fprint][-1,fprint0][-1,fprintf]' -f - \
'c[-1,-fstype]' -k '(ufs 4.2 4.3 nfs tmp mfs S51K S52K)' - \
'c[-1,-type]' -k '(b c d p f l s)' - \
'c[-1,-group]' -s '$(groups)' - \
'c[-1,-user]' -u - \
'r[-exec,;][-ok,;]' -l '' -- find

# Default completion.  See func/multicomp
compctl -D -f + -U -K multicomp

groups()
{
    reply=( `(cat /etc/group;ypcat group) | awk -F: '{print $1}' | sort | uniq` )
}

#   Miscellaneous
compctl -g "*.[cCoa]" -x 's[-I]' -g "*(/)" - \
  's[-l]' -s '${(s.:.)LD_LIBRARY_PATH}/lib*.a(:t:r:s/lib//)' -- cc gcc

# If tar specifies 'compressed' then glob on .tar.gz or .tgz files. Otherwise
# glob on .tar files.
compctl -f -x \
    'C[-1,(*z*f*|*f*z*)] p[2]' -g "*.(tar.gz|tgz)" + -g '*(-/)' - \
    'C[-1,(*j*f*|*j*z*)] p[2]' -g "*.tar.bz2" + -g '*(-/)' - \
    'C[-1,*f*] p[2]' -g "*.tar" + -g '*(-/)' -- tar

compctl -g '*.zip' + -g '*(-/)' unzip

# Some builtins.
compctl -j -P % fg bg wait jobs disown
compctl -A shift
compctl -caF type whence which
compctl -F unfunction
compctl -a unalias
compctl -v unset typeset declare vared readonly export integer
compctl -e disable
compctl -d enable
compctl -k '(cputime filesize datasize stacksize coredumpsize resident \
  memoryuse memorylocked descriptors openfiles vmemorysize)' limit ulimit
compctl -l '' -x 'p[1]' -f -- . source
compctl -f -x 'p[1]' -c -- function whence which
compctl -f -x 'p[1]' -v -- set
compctl -F func functions unfunc unfunction
compctl -g '.*(-/) *(-/)' cd _dtymon_cd
compctl -f -x 'p[1]' -R -- alias unalias
compctl -v -P '$' echo
compctl -f -x 'C[-1,(-f)]' -g "[Mm]akefile* *.mk *.mak" -- make
